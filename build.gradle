plugins {
    id 'java-library'
    id 'maven-publish'
}

dependencies {

    // https://mvnrepository.com/artifact/javax.ws.rs/javax.ws.rs-api
    implementation group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0'
    // https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.10.0'
    // https://mvnrepository.com/artifact/com.squareup.okhttp3/logging-interceptor
    implementation group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: '4.10.0'
    // https://mvnrepository.com/artifact/org.apache.oltu.oauth2/org.apache.oltu.oauth2.client
    implementation group: 'org.apache.oltu.oauth2', name: 'org.apache.oltu.oauth2.client', version: '1.0.2'
    // https://mavenlibs.com/maven/dependency/com.google.code.gson/gson
    implementation 'com.google.code.gson:gson:2.10'
    // https://mavenlibs.com/maven/dependency/io.gsonfire/gson-fire
    implementation group: 'io.gsonfire', name: 'gson-fire', version: '1.8.5'
    // https://mavenlibs.com/maven/dependency/org.openapitools/jackson-databind-nullable
    implementation 'org.openapitools:jackson-databind-nullable:0.2.4'
    // https://mvnrepository.com/artifact/javax.annotation/javax.annotation-api
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    // https://mvnrepository.com/artifact/com.google.code.findbugs/jsr305
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
}


// Publishing options

group = 'com.aspose'
version = '22.12.0'

// probably is correct to use secondary packages
java {
   withJavadocJar()
   withSourcesJar()
}

publishing {
    publications {
        mavenAndroid(MavenPublication) {
            artifactId = 'aspose-ocr-cloud-android'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'aspose-ocr-cloud-android'
                description = 'Aspose cloud Android (java) SDK'
                url = 'https://products.aspose.cloud/ocr/android'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/aspose-ocr-cloud/aspose-ocr-cloud-android/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'asudakov'
                        name = 'Aleksandr Sudakov'
                        email = 'alexandr.sudakov@aspose.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:aspose-ocr-cloud/aspose-ocr-cloud-android.git'
                    developerConnection = 'scm:git:git@github.com:aspose-ocr-cloud/aspose-ocr-cloud-android.git'
                    url = 'https://github.com/aspose-ocr-cloud/aspose-ocr-cloud-android/'
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://releases.aspose.cloud/java/repo/'
            name = 'AsposeCloudJavaRepository'
        }
    }
}
// Documentation generation
javadoc {
   if(JavaVersion.current().isJava9Compatible()) {
       options.addBooleanOption('html5', true)
   }   
    options.addStringOption('tag', 'http.response.details:a:HTTP response details:')
}


// Jar task extension - adding new data to META-INF.MF
tasks.named('jar') {
    manifest {
        attributes('Implementation-Title': project.name,
                   'Implementation-Version': project.version)
    }
}

